grammar org.eclipse.xpect.Xpect hidden()

generate xpect "http://www.eclipse.org/xpect/Xpect"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

XpectFile:
	(text+=Text ignore=Ignore)? 
	(text+=Text setup=Lexicalspace_Setup)?
	(text+=Text invocation+=Lexicalspace_Invocation)*
	(text+=Text)?
;
	
Lexicalspace_Setup returns Setup hidden(WS, ML_COMMENT, SL_COMMENT):
	{Setup} "XPECT_CLASS" test=[types::JvmDeclaredType|QualifiedName] 
	("{" assignemnts+=Assignment* "}")?
	"END_CLASS"
;
	
Assignment:
	element=[types::JvmMember|ID] "=" value=Literal
;

Literal:
	{BooleanLiteral} (value?="true" | "false") |
	{IntLiteral} value=INT |
	{StringLiteral} value=STRING |
	{Component} type=[types::JvmDeclaredType|QualifiedName]? ("{" assignemnts+=Assignment* "}")
;

Ignore returns Ignore hidden():
	{Ignore} "XPECT_IGNORE";

Lexicalspace_Invocation returns XpectInvocation hidden(WS, ML_COMMENT, SL_COMMENT):
	"XPECT" ignore?="!"? element=[types::JvmIdentifiableElement|INVOCATION];
	
Text:
	ANY_OTHER+
;

QualifiedName:
	ID ("." ID)*;

terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal INVOCATION : '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal INT returns ecore::EInt: ('0'..'9')+;
terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
			"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;
terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;